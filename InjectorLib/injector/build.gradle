buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
        classpath "com.github.dcendents:android-maven-plugin:1.2"
    }
}

apply plugin: "com.android.library"

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//////////////////////////////// Upload to BinTray ////////////////////////////////
group = "com.azoft.injector"
final def siteUrl = 'https://github.com/mig35/InjectorLib'
final def gitUrl = "https://github.com/mig35/InjectorLib.git"
// This is the library version used when deploying the artifact
version = "0.9.3"

final Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ["archives"]
    pkg {
        repo = "maven"
        name = "injector"
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'

            // Add your description here
            name "Injector for data save state in Android. Description is here: https://github.com/mig35/InjectorLib"
            url siteUrl

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'azoft'
                    name 'azoft'
                    email 'android-mobile@azoft.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }
    }
}
//////////////////////////////// Upload to BinTray ////////////////////////////////

android {
    compileSdkVersion 23
    buildToolsVersion "24"
    resourcePrefix "injector__"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    configurations {
        javadocDeps
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    javadocDeps 'com.android.support:support-annotations:23.4.0'
    compile 'com.android.support:support-annotations:23.4.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

// Excluding aar artifacts from uploading
configurations.archives.artifacts.with { archives ->
    for (def iter = archives.iterator(); iter.hasNext();) {
        if (iter.next().file =~ 'aar') iter.remove();
    }
}